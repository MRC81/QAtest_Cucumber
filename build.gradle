plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group 'tst.test'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14
}

ext {
    selenideVersion = '6.16.0'
    junitVersion = "5.9.3"
    cucumberVersion = "7.12.1"
    allureVersion= "2.21.0"
}

allure {
    report {
        version.set(allureVersion)
        reportDir.set(project.reporting.baseDirectory.dir("allure-report"))
    }
    adapter {
        autoconfigureListeners.set(true)
        aspectjWeaver.set(true)

        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
            cucumber7Jvm
        }
    }
}


dependencies {
    implementation (
            "org.junit.jupiter:junit-jupiter-api:$junitVersion",
            "org.junit.jupiter:junit-jupiter-params:$junitVersion",
            "com.codeborne:selenide:$selenideVersion",
            "io.cucumber:cucumber-java:$cucumberVersion",
            "io.cucumber:cucumber-junit:$cucumberVersion",
            "io.cucumber:cucumber-picocontainer:$cucumberVersion",
            "io.qameta.allure:allure-cucumber7-jvm:$allureVersion",
            "io.qameta.allure:allure-junit5:$allureVersion",
            "org.junit.vintage:junit-vintage-engine:${junitVersion}",
            "org.slf4j:slf4j-simple:2.0.7"
    )

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

tasks.register('cucumber') {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'pretty',
                    '--plugin', 'html:reports/cucumber-report.html',
                    '--plugin', 'io.qameta.allure.cucumber7jvm.AllureCucumber7Jvm',
                    '--glue', 'steps',
                    '--glue', 'hooks',
                    '--publish',
                    'src/test/resources/features']
        }
    }
}

test {
    dependsOn 'cucumber'
    useJUnitPlatform()
    systemProperties(project.gradle.startParameter.systemPropertiesArgs)

    testLogging {
        events "PASSED", "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
    }

    testLogging.showStandardStreams = true
}
